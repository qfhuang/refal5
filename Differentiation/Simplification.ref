$EXTERNAL Contain;
$EXTERNAL IsNothing;
$EXTERNAL IsVariable;
$EXTERNAL FormatTopLevelSimpleExpression;
$EXTERNAL NoS12n;
$EXTERNAL S12n_SimpleExpression;

$ENTRY S12n_SimplePresentation
{
    t.SimpleExpr =
        <S12n_SimplePresentation_Internal
            (
                (
                    NoS12n
                    RemoveZeroPower_Monomial
                    RemoveZeroPower_NonMonomialFraction
                )

                (
                    SearchZeroMult_SimpleSummand
                    NoS12n
                    NoS12n
                )

                (
                    NoS12n
                    NoS12n
                    RemoveOnenessPower_NonMonomialFraction
                )
            )

            t.SimpleExpr
        >;

    e.X =;
}

S12n_SimplePresentation_Internal
{
    (t.S12n_Functions e.X) t.SimpleExpr =
        <S12n_SimplePresentation_Internal
            (e.X)
            <S12n_SimpleExpression
                t.S12n_Functions
                t.SimpleExpr
            >
        >;

    () t.SimpleExpr = t.SimpleExpr;
}

$ENTRY SearchZeroMult_SimpleSummand
{
    ((0 t.D) t.Monomial_1 t.NonMonomial_1),
    <Contain (0) t.D> : False =;

    t.X = t.X;
}

FormatSomething
{
    s.Variable,
    <IsVariable s.Variable> : True =
        <Explode s.Variable>;

    t.Something,
    <FormatTopLevelSimpleExpression t.Something> : e.Formatted,
    <IsNothing e.Formatted> : False =
        e.Formatted;
}

/* TODO: move Prout to Main.ref */
/* TODO: maybe interactive ask user, use restriction or not? */
AddRestriction
{
    t.Something =
        <Prout
            'Restriction: '
            <FormatSomething t.Something>
            ' != ' '0'
        >;
}

$ENTRY RemoveZeroPower_Monomial
{
    (e.X (s.Variable 0) e.Y) =
        <AddRestriction s.Variable>
        <RemoveZeroPower_Monomial (e.X e.Y)>;

    t.Monomial = t.Monomial;
}

IsObviousZeroSimpleExpr
{
    (((0 t.D) t.Monomial t.NonMonomial)),
    <Contain (0) t.D> : False = True;

    e.X = False;
}

$ENTRY RemoveZeroPower_NonMonomialFraction
{
    ((e.X (Invol t.SimpleExpr 0) e.Y) t.D),
    <IsObviousZeroSimpleExpr t.SimpleExpr> : False =
        <AddRestriction t.SimpleExpr>
        <RemoveZeroPower_NonMonomialFraction ((e.X e.Y) t.D)>;

    (t.N (e.X (Invol t.SimpleExpr 0) e.Y)),
    <IsObviousZeroSimpleExpr t.SimpleExpr> : False =
        <AddRestriction t.SimpleExpr>
        <RemoveZeroPower_NonMonomialFraction (t.N (e.X e.Y))>;

    t.NonMonomialFrac = t.NonMonomialFrac;
}

$ENTRY RemoveOnenessPower_NonMonomialFraction
{
    ((e.X (Invol t.SimpleExpr 1) e.Y) t.D) =
        <RemoveOnenessPower_NonMonomialFraction
            ((e.X t.SimpleExpr e.Y) t.D)
        >;

    (t.N (e.X (Invol t.SimpleExpr 1) e.Y)) =
        <RemoveOnenessPower_NonMonomialFraction
            (t.N (e.X t.SimpleExpr e.Y))
        >;

    t.NonMonomialFrac = t.NonMonomialFrac;
}

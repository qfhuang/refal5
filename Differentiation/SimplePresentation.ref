$EXTERNAL IsMacrodigit;
$EXTERNAL IsVariable;
$EXTERNAL IsFunctionName;
$EXTERNAL IsConstNumber;
$EXTERNAL NegConstNumber;
$EXTERNAL SubConstNumber;
$EXTERNAL AddConstNumber;
$EXTERNAL Contain;

$ENTRY SimplePresentation
{
    t.Expr = <SimpleExpression t.Expr>;

    e.X =;
}

* s.N is numerator.
* s.D is denominator.
NegConstMult
{
    (0 s.D) = (0 s.D);
    (MonadicMinus s.N s.D) = (s.N s.D);
    (s.N s.D) = (MonadicMinus s.N s.D);
}

$ENTRY NegSimpleSummand
{
    (t.ConstMult t.Monomial t.NonMonomialFrac) =
        (<NegConstMult t.ConstMult> t.Monomial t.NonMonomialFrac);
}

SimpleExpression
{
    (e.Result) (Plus t.Expr t.Expr_2) =
        <SimpleExpression (<SimpleSummand t.Expr_2> e.Result) t.Expr>;

    (e.Result) (Minus t.Expr t.Expr_2) =
        <SimpleExpression
        (<NegSimpleSummand <SimpleSummand t.Expr_2>> e.Result)
        t.Expr>;

    (e.Result) t.Expr_2 = (<SimpleSummand t.Expr_2> e.Result);

    t.Expr = <SimpleExpression () t.Expr>;
}

SimpleSummand
{
    t.Summand (Mul t.Expr_2 t.Expr_1) =
        <SimpleSummand
            <SimpleSummandMul t.Summand <SimpleExpr_1 t.Expr_1>>
            t.Expr_2
        >;

    t.Summand (Div t.Expr_2 t.Expr_1) =
        <SimpleSummand
            <SimpleSummandDiv t.Summand <SimpleExpr_1 t.Expr_1>>
            t.Expr_2
        >;

    t.Summand t.Expr_1 =
        <SimpleSummandMul t.Summand <SimpleExpr_1 t.Expr_1>>;

    t.Expr_2 = <SimpleSummand ((1 1) () (() ())) t.Expr_2>;
}

MulConstMult_SimpleExpr_0
{
    (MonadicMinus s.N1 s.D1) (s.N2 s.D2),
    <IsMacrodigit s.N2> : True,
    <IsMacrodigit s.D2> : True =
        (MonadicMinus
            <Mul (s.N1) s.N2>
            <Mul (s.D1) s.D2>
        );

    (s.N1 s.D1) (s.N2 s.D2),
    <IsMacrodigit s.N2> : True,
    <IsMacrodigit s.D2> : True =
        (
            <Mul (s.N1) s.N2>
            <Mul (s.D1) s.D2>
        );

    t.ConstMult t.SimpleExpr_0 =
        t.ConstMult;
}

MulMonomial_SimpleExpr_0
{
    (e.X (s.Variable t.ConstPower_1) e.Y) (s.Variable t.ConstPower_2),
    <IsVariable s.Variable> : True =
        (e.X
        (s.Variable <AddConstNumber (t.ConstPower_1) t.ConstPower_2>)
        e.Y);

    (e.X) (s.Variable t.ConstPower),
    <IsVariable s.Variable> : True =
        ((s.Variable t.ConstPower) e.X);

    t.Monomial t.SimpleExpr_0 = t.Monomial;
}

DivMonomial_SimpleExpr_0
{
    (e.X (s.Variable t.ConstPower_1) e.Y) (s.Variable t.ConstPower_2),
    <IsVariable s.Variable> : True =
        (e.X
        (s.Variable <SubConstNumber (t.ConstPower_1) t.ConstPower_2>)
        e.Y);

    (e.X) (s.Variable t.ConstPower),
    <IsVariable s.Variable> : True =
        ((s.Variable <NegConstNumber t.ConstPower>) e.X);

    t.Monomial t.SimpleExpr_0 = t.Monomial;
}

IsVarPower
{
    (s.Variable t.ConstPower),
    <IsVariable s.Variable> : True,
    <IsConstNumber t.ConstPower> : True =
        True;

    t.Any = False;
}

MulNonMonomial_SimpleExpr_0
{
    (e.X) t.SimpleExpr_0,
    <IsConstNumber t.SimpleExpr_0> : False,
    <IsVarPower t.SimpleExpr_0> : False =
        (t.SimpleExpr_0 e.X);

    t.NonMonomial t.SimpleExpr_0 = t.NonMonomial;
}

MulNonMonomialFraction_SimpleExpr_0
{
    (t.N1 t.D1) (t.N2 t.D2) =
        (
            <MulNonMonomial_SimpleExpr_0 t.N1 t.N2>
            <MulNonMonomial_SimpleExpr_0 t.D1 t.D2>
        );
}

SimpleSummandMul
{
    (t.ConstMult t.Monomial t.NonMonomialFrac) t.SimpleExpr_1,
    t.SimpleExpr_1 : (MonadicMinus t.SimpleExpr_0) =
        <SimpleSummandMul
            (<NegConstNumber t.ConstMult> t.Monomial t.NonMonomialFrac)
            t.SimpleExpr_0
        >;

    (t.ConstMult t.Monomial t.NonMonomialFrac) t.SimpleExpr_0 =
        (
            <MulConstMult_SimpleExpr_0
                t.ConstMult
                (t.SimpleExpr_0 1)
            >
            <MulMonomial_SimpleExpr_0
                t.Monomial
                t.SimpleExpr_0
            >
            <MulNonMonomialFraction_SimpleExpr_0
                t.NonMonomialFrac
                (t.SimpleExpr_0 1)
            >
        );
}

SimpleSummandDiv
{
    (t.ConstMult t.Monomial t.NonMonomialFrac) t.SimpleExpr_1,
    t.SimpleExpr_1 : (MonadicMinus t.SimpleExpr_0) =
        <SimpleSummandDiv
            (<NegConstNumber t.ConstMult> t.Monomial t.NonMonomialFrac)
            t.SimpleExpr_0
        >;

    (t.ConstMult t.Monomial t.NonMonomialFrac) t.SimpleExpr_0 =
        (
            <MulConstMult_SimpleExpr_0
                t.ConstMult
                (1 t.SimpleExpr_0)
            >
            <DivMonomial_SimpleExpr_0
                t.Monomial
                t.SimpleExpr_0
            >
            <MulNonMonomialFraction_SimpleExpr_0
                t.NonMonomialFrac
                (1 t.SimpleExpr_0)
            >
        );
}

SimpleExpr_1
{
    (MonadicMinus (MonadicMinus t.Expr_1)) =
        <SimpleExpr_1 t.Expr_1>;

    (MonadicMinus t.Expr_0) = (MonadicMinus <SimpleExpr_0 t.Expr_0>);

    t.Expr_0 = <SimpleExpr_0 t.Expr_0>;
}

SimpleExpr_0
{
    s.Macrodigit,
    <IsMacrodigit s.Macrodigit> : True =
        s.Macrodigit;

    s.Variable,
    <IsVariable s.Variable> : True =
        (s.Variable 1);

    /* Only variables, not all expressions. */
    (Invol s.Variable t.ConstPower),
    <IsVariable s.Variable> : True =
        (s.Variable t.ConstPower);

    (Invol t.Expr t.ConstPower) =
        (Invol <SimpleExpression t.Expr> t.ConstPower);

    (s.FuncName t.Expr),
    <IsFunctionName s.FuncName> : True =
        (s.FuncName <SimpleExpression t.Expr>);

    t.Expr =
        <SimpleExpression t.Expr>;
}
